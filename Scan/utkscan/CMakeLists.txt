#CMake file for UTKScan.

option(BUILD_UTKSCAN_TESTS "Build unit tests for utkscan" ON)
option(UTKSCAN_GAMMA_GATES "Gamma-Gamma gates in GeProcessor" OFF)
option(USE_GSL "Use GSL for Pulse Fitting" ON)
option(UTKSCAN_ONLINE "Options for online scans" OFF)
option(UTKSCAN_TREE_DEBUG "Debugging info for TreeCorrelator" OFF)
option(UTKSCAN_VERBOSE "Make Scan More Verbose" OFF)

#Find the UnitTest++ Package. This package can be obtained from
#https://github.com/unittest-cpp/unittest-cpp.git
if(BUILD_UTKSCAN_TESTS)
    find_package(UnitTest++ REQUIRED)
    link_directories(${UNITTEST++_LIBRARY_DIR})
    include_directories(${UNITTEST++_INCLUDE_DIR})
endif(BUILD_UTKSCAN_TESTS)

# newreadout is needed to account for a change to pixie16 readout
# structure change on 03/20/08. Is is REQUIRED!!
add_definitions(-D newreadout)

#utkscan will have Gamma-Gamma gating in the GeProcessor
if(UTKSCAN_GAMMA_GATES)
    add_definitions(-D GGATES)
endif(UTKSCAN_GAMMA_GATES)

#utkscan will be streamlined for online processing
if(UTKSCAN_ONLINE)
    add_definitions(-D ONLINE)
endif(UTKSCAN_ONLINE)

#utkscan will have debugging for the TreeCorrelator
if(UTKSCAN_TREE_DEBUG)
    add_definitions(-D TREE_DEBUG)
endif(UTKSCAN_TREE_DEBUG)

#utkscan will be more verbose in its output
if(UTKSCAN_VERBOSE)
    add_definitions(-D VERBOSE)
endif(UTKSCAN_VERBOSE)

#Check if GSL is installed
if(USE_GSL)
    find_package(GSL REQUIRED)
    add_definitions("-D usegsl")
endif(USE_GSL)

#------------------------------------------------------------------------------

#Add the local include directories to the build tree
include_directories(
        analyzers/include
        core/include
        experiment/include
        processors/include)

#Add the Analyzers to the build
add_subdirectory(analyzers)
#Add the core classes to the build
add_subdirectory(core)
#Add the experiment specific processors to the build
add_subdirectory(experiment)
#Add the processors to the build
add_subdirectory(processors)

#------------------------------------------------------------------------------

#Create utkscan program
if(NOT USE_HRIBF)
    set(SCAN_NAME utkscan)
    add_executable(${SCAN_NAME}
            $<TARGET_OBJECTS:MainObjects>
            $<TARGET_OBJECTS:CoreObjects>
            $<TARGET_OBJECTS:AnalyzerObjects>
            $<TARGET_OBJECTS:ProcessorObjects>
            $<TARGET_OBJECTS:ExperimentObjects>)
else(USE_HRIBF)
    set(SCAN_NAME utkscanor)
    add_executable(${SCAN_NAME}
            $<TARGET_OBJECTS:MainObjects>
            $<TARGET_OBJECTS:CoreObjects>
            $<TARGET_OBJECTS:AnalyzerObjects>
            $<TARGET_OBJECTS:ProcessorObjects>
            $<TARGET_OBJECTS:ExperimentObjects>
            $<TARGET_OBJECTS:ScanorObjects>)
    target_link_libraries(utkscanor ${HRIBF_LIBRARIES})
endif(NOT USE_HRIBF)

#Add libraries to be linked with utkscan
target_link_libraries(${SCAN_NAME} ${LIBS} ScanStatic)

#If we have GSL installed link
if(USE_GSL)
    target_link_libraries(${SCAN_NAME} ${GSL_LIBRARIES})
endif(USE_GSL)

#If ROOT is installed we'll link
if(USE_ROOT)
    target_link_libraries(${SCAN_NAME} ${ROOT_LIBRARIES})
endif(USE_ROOT)

#------------------------------------------------------------------------------

#Install utkscan to the bin directory
install(TARGETS ${SCAN_NAME} DESTINATION bin)
#Install configuration files to the share directory
install(DIRECTORY share/utkscan DESTINATION share)

#------------------------------------------------------------------------------